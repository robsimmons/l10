/* L10 Parser */

name L10Parse 

terminal LCURLY of pos
terminal RCURLY of pos
terminal LPAREN of pos
terminal RPAREN of pos
terminal EX of pos
terminal PERIOD of pos

terminal COLON of pos   precl 20 /* nonassoc */
terminal EQ of pos      precl 20 /* nonassoc */
terminal LARROW of pos  precl 30
terminal RARROW of pos  precr 40 
terminal COMMA of pos   precl 50
terminal AT of pos      precl 60 /* nonassoc */

terminal EQEQ of pos    precl 70 /* nonassoc */
terminal NEQ of pos     precl 70 /* nonassoc */
terminal GT of pos      precl 70 /* nonassoc */
terminal LT of pos      precl 70 /* nonassoc */
terminal GEQ of pos     precl 70 /* nonassoc */
terminal LEQ of pos     precl 70 /* nonassoc */

terminal PLUS of pos    precl 80 
terminal MINUS of pos   precl 80

terminal WORLD of pos
terminal TYPE of pos
terminal REL of pos
terminal USCORE of pos

terminal UCID of pos_str
terminal LCID of pos_str
terminal NUM of pos_int
terminal STRING of pos_str

terminal LANNO of pos
terminal RANNO of pos

start Decl

nonterminal Decl : decl =
   1:Syn 2:PERIOD               => full_decl
   LANNO 1:Anno RANNO           => anno

nonterminal Bind : syn =
   1:UCID COLON 2:Syn           => ascribe_ucid 

nonterminal Syn : syn =
   1:LCID COLON 2:Syn           => Ascribe
   1:LCID EQ 2:Syn              => Assign      
   1:Syn LARROW 2:Syn           => Arrow      
   1:Syn RARROW 2:Syn           => rarrow      
   1:Syn COMMA 2:Syn            => Conj        
   1:Syn AT 2:Syn               => At          
   1:Syn EQEQ 2:Syn             => eqeq        
   1:Syn NEQ 2:Syn              => neq         
   1:Syn GT 2:Syn               => gt          
   1:Syn LT 2:Syn               => lt          
   1:Syn GEQ 2:Syn              => geq         
   1:Syn LEQ 2:Syn              => leq         
   1:Syn PLUS 2:Syn             => plus

   1:Simp                       => id1
   1:LCID 2:Sings               => App
   1:LCURLY 2:Bind RCURLY 3:Syn => Pi             precr 10
   1:EX 2:UCID PERIOD 3:Syn     => Ex             precr 10

nonterminal Simp : syn =
   1:LPAREN 2:Syn 3:RPAREN      => id2
   1:UCID                       => Var
   1:USCORE                     => Uscore
   1:WORLD                      => World
   1:TYPE                       => Type
   1:REL                        => Rel
   1:NUM                        => Num
   1:STRING                     => String

nonterminal Sings : sings =     => sings_end
   1:Simp 2:Sings               => sings_cons
   1:LCID 2:Sings               => sings_lcid



/* Extensions for the annotation language */

terminal ANNO_QUERY of pos

nonterminal Modes : modes =     => mode_end
   PLUS 1:Modes                 => mode_input
   MINUS 1:Modes                => mode_output
   USCORE 1: Modes              => mode_ignore

nonterminal Anno : decl =
   ANNO_QUERY 1:LCID COLON 2:LCID 3:Modes => anno_query

