/* L10 Lexer */

name L10Lex
alphabet 128

set id = (| (range 'A 'Z 'a 'z '0 '9) '_ '\ '')
set char = (| (range 'A 'Z 'a 'z '0 '9) 32 '' '! '@ '# '$ '% '^ '& '* '( '))
set white = (| 32 9) 
set pos = (range '1 '9)
set digit = (range '0 '9)
set az = (range 'a 'z)
set AZ = (range 'A 'Z)

regexp lcid = (seq az (* id))
regexp ucid = (seq AZ (* id))
regexp string = (seq '" (* char) '")
regexp decnum = (| '0 (seq pos (* digit)))
regexp eol = (| (seq 13 (? 10)) 10) /* Three end-of-lines: \n\r, \n, and \r */

function lexmain : tok = 
   eos       => eof
   (+ white) => space
   eol       => newline

   '{        => lcurly
   '}        => rcurly
   '(        => lparen
   ')        => rparen
   "Ex"      => ex

   '.        => period
   ',        => comma
   ':        => colon
   '=        => eq
   "->"      => rarrow
   "<-"      => larrow
   '@        => at
   
   "=="      => eqeq
   "!="      => neq
   '>        => gt
   '<        => lt
   ">="      => geq
   "<="      => leq
   '+        => plus
   
   "world"   => world
   "type"    => ty
   "rel"     => rel
   '_        => uscore
   
   ucid      => ucid
   lcid      => lcid
   decnum    => num
   string    => str
   
   "--"      => linecomment
   "{-"      => comment
   "{-#"     => anno_start
   epsilon   => error

function anno : tok = 
   (+ white) => anno_space
   eol       => anno_newline

   "QUERY"   => anno_query

   '+        => anno_plus
   '-        => anno_minus
   '_        => anno_uscore
   lcid      => anno_lcid

   "#-}"     => anno_end
   "--"      => anno_linecomment
   "{-"      => anno_comment
   epsilon   => anno_error

function linecomment : u = 
   eol       => linecomment_close
   any       => linecomment_skip
   epsilon   => linecomment_error
   
function comment : u = 
   "{-"      => comment_open
   "-}"      => comment_close
   eol       => comment_newline
   any       => comment_skip
   epsilon   => comment_error

